plugins {
    `java-library`
    id("com.vanniktech.maven.publish") version "0.33.0"
    kotlin("jvm") version "2.3.0" apply false
}

val baseVersion = "4.0.0"
val releaseSnapshots = true
val isSnapshot = System.getenv("SNAPSHOT_BUILD") == "true"
val rootJavaVersion = 21

tasks.register("printReleaseSnapshots") {
    doLast {
        println("releaseSnapshots=$releaseSnapshots")
    }
}

tasks.register("printVersion") {
    doLast {
        println("baseVersion=$baseVersion")
    }
}

allprojects {
    tasks.withType<JavaCompile> {
        options.encoding = "UTF-8"
    }

    group = "studio.mevera"
    version = baseVersion

    if (isSnapshot && releaseSnapshots) {
        version = "$version-SNAPSHOT"
    }

    extra.apply {
        val kyoriVersion = "5.1.1"
        val kyoriPlatformVersion = "4.4.1"

        set("kyori", fun(module: String): String {
            return "net.kyori:adventure-$module:$kyoriVersion"
        })

        set("kyoriPlatform", fun(module: String): String {
            return "net.kyori:adventure-$module:$kyoriPlatformVersion"
        })

        set(
            "KyoriModule", mapOf(
                "API" to "api",
                "MINI_MESSAGE" to "text-minimessage",
                "BUKKIT" to "platform-bukkit",
                "BUNGEE" to "platform-bungeecord",
                "SPONGE" to "platform-spongeapi"
            )
        )
    }
}

java {
    val javaVersion = JavaVersion.toVersion(rootJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    toolchain.languageVersion.set(JavaLanguageVersion.of(rootJavaVersion))
}

tasks.withType<JavaCompile>().configureEach {
    options.release.set(rootJavaVersion)
}

subprojects {
    apply(plugin = "java-library")

    tasks.withType<JavaCompile> {
        options.encoding = "UTF-8"
    }

    apply(plugin = "com.vanniktech.maven.publish")

    configure<com.vanniktech.maven.publish.MavenPublishBaseExtension> {
        coordinates(group as String, "imperat-${name}", version as String)

        pom {
            name.set("Imperat")
            description.set("A modern customizable command framework.")
            inceptionYear.set("2024")
            url.set("https://github.com/MeveraStudios/Imperat/")
            licenses {
                license {
                    name.set("MIT")
                    url.set("https://opensource.org/licenses/MIT")
                    distribution.set("https://mit-license.org/")
                }
            }
            developers {
                developer {
                    id.set("mqzn")
                    name.set("Mqzn")
                    url.set("https://github.com/Mqzn/")
                }
                developer {
                    id.set("iiahmedyt")
                    name.set("iiAhmedYT")
                    url.set("https://github.com/iiAhmedYT/")
                }
            }
            scm {
                url.set("https://github.com/MeveraStudios/Imperat/")
                connection.set("scm:git:git://github.com/MeveraStudios/Imperat.git")
                developerConnection.set("scm:git:ssh://git@github.com/MeveraStudios/Imperat.git")
            }
        }

        if (!gradle.startParameter.taskNames.any { it == "publishToMavenLocal" }
            && (!isSnapshot || (isSnapshot && releaseSnapshots))) {
            publishToMavenCentral()
            signAllPublications()
        }

        tasks.withType<JavaCompile> {
            options.encoding = "UTF-8"
            options.compilerArgs.add("-parameters")
        }
    }
}
