plugins {
    id("com.gradleup.shadow") version "8.3.9"
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://repo.codemc.io/repository/nms/")
    }
    maven("https://hub.spigotmc.org/nexus/content/groups/public/")
    maven {
        url = uri("https://hub.spigotmc.org/nexus/content/repositories/snapshots/")

        content {
            includeGroup("org.bukkit")
            includeGroup("org.spigotmc")
        }
    }
    maven { url = uri("https://oss.sonatype.org/content/repositories/snapshots") }
    maven { url = uri("https://oss.sonatype.org/content/repositories/central") }
    maven {
        name = "papermc"
        url = uri("https://repo.papermc.io/repository/maven-public/")
    }
    maven {
        url = uri("https://libraries.minecraft.net")
    }

}

fun kyoriPlatform(module: String): String {
    return (rootProject.extra["kyoriPlatform"] as (String) -> String).invoke(module)
}

@Suppress("UNCHECKED_CAST")
val KyoriModule = rootProject.extra["KyoriModule"] as Map<String, String>

dependencies {
    api(project(":adventure"))
    api(project(":brigadier"))

    compileOnly(project(":core"))

    compileOnly("com.mojang:brigadier:1.0.18")
    // Modern Paper API (1.21.4+) — required for the modern backend's
    // Brigadier integration. Class-loaded at runtime only after detection,
    // so plugins on legacy Spigot do NOT need paper-api on their classpath.
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
    compileOnly("org.spigotmc:spigot:1.13.2-R0.1-SNAPSHOT")

    compileOnly(kyoriPlatform(KyoriModule["BUKKIT"]!!))

    // Test dependencies
    testImplementation(project(":core"))
    testImplementation(project(":adventure"))
    testImplementation(project(":brigadier"))
    testImplementation("com.mojang:brigadier:1.0.18")
    testImplementation("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")
    testImplementation(kyoriPlatform(KyoriModule["BUKKIT"]!!))
    testImplementation("org.junit.jupiter:junit-jupiter:5.10.1")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher:1.10.1")
    testImplementation("org.mockbukkit.mockbukkit:mockbukkit-v1.21:4.101.0")
}

tasks.processTestResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.test {
    useJUnitPlatform()
}

val targetJavaVersion = 21
java {
    val javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion.set(JavaLanguageVersion.of(targetJavaVersion))
    }
}

tasks.withType<JavaCompile>().configureEach {
    options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible) {
        options.release.set(targetJavaVersion)
    }
}

// MockBukkit v1.21 requires Java 21 at runtime and compile time
tasks.named<JavaCompile>("compileTestJava") {
    options.release.set(21)
    javaCompiler.set(javaToolchains.compilerFor {
        languageVersion.set(JavaLanguageVersion.of(21))
    })
}

tasks.test {
    javaLauncher.set(javaToolchains.launcherFor {
        languageVersion.set(JavaLanguageVersion.of(21))
    })
}

